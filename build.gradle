plugins {
    id 'jacoco'
    id 'java-library'
    id 'java-library-distribution'
}

allprojects {
    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}

dependencies {
    implementation project(':scraper-nodes-dev')
    implementation project(':scraper-nodes-erlang')
    implementation project(':scraper-nodes-server')
    implementation project(':scraper-nodes-unstable')
}

subprojects { 
    configurations {
        providedCompile
    }

    afterEvaluate {
        dependencies {
            providedCompile 'com.github.scraperflow.scraper:api:v0.17.1'

            testImplementation 'com.github.scraperflow.scraper:core:v0.17.1'
            testImplementation 'com.github.scraperflow.scraper:core-plugins:v0.17.0'

            testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.1'
            testImplementation 'org.junit.jupiter:junit-jupiter-params:5.6.1'
            testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
        }

        compileJava {
            sourceSets.main.compileClasspath += configurations.providedCompile
        }
    }

    group = 'org.scraper'

    plugins.withType(JavaPlugin).configureEach {
        java {
            modularity.inferModulePath = true
        }
    }
    
    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }
}

// merge coverage
apply from: 'gradle/jacoco-merge.gradle'

// local install
task install(type: Copy) { 
    def homePath = System.properties['user.home']
    from("${buildDir}/install/scraper-nodes/lib")
    into("${homePath}/opt/lib/scraper/var")
    include('*.jar')
}
install.dependsOn(installDist);
